buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

version "4.3.0.4"
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.grails-gsp"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://www.it-jw.com/maven" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    console "org.grails:grails-console"
    compile 'org.eclipse.birt.runtime:org.eclipse.birt.runtime:4.3.0a'
    
    // necessary to prevent Error
    // Caused by: java.lang.NoSuchMethodError: org.mozilla.javascript.Parser.parse(Ljava/lang/String;Ljava/lang/String;I)Lorg/mozilla/javascript/ScriptOrFnNode;
    compile group: 'rhino', name: 'js', version: '1.7R1'
    
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-plugin-testing"
}

allprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}

// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

grailsPublish {
    user = "$publishUser"
    key = "$publishKey"
    githubSlug = 'paulwellnerbou/grails-birt-plugin'
    repo = 'maven'
    userOrg = 'paulwellnerbou'
    license {
        name = 'Apache-2.0'
    }
    title = "BIRT Reporting Plugin"
    desc = "Makes it easy to integrate the BIRT reporting engine (runtime component) into your Grails application."
    developers = [paulwellnerbou: "Paul Wellner Bou"]
}

